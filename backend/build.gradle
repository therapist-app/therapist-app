plugins {
	id 'java'
	id 'idea'
	id 'org.springframework.boot' version '3.3.0-SNAPSHOT'
	id 'io.spring.dependency-management' version '1.1.4'
	id "org.sonarqube" version "6.0.1.5171"
	id 'jacoco'
	id 'com.github.johnrengelman.processes' version '0.5.0'
	id 'com.diffplug.spotless' version '6.22.0'
	id 'org.openapi.generator' version '7.4.0'
}

group = 'ch.uzh.ifi.imrg'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion.set(JavaLanguageVersion.of(21))
	}
}

configurations {
	developmentOnly
	runtimeClasspath {
		extendsFrom developmentOnly
	}
}

repositories {
	mavenCentral()
	maven { url 'https://repo.spring.io/milestone' }
	maven { url 'https://repo.spring.io/snapshot' }
}

springBoot {
	mainClass.set('ch.uzh.ifi.imrg.platform.Application')
}

dependencies {
	compileOnly 'org.projectlombok:lombok:1.18.30'
	annotationProcessor 'org.projectlombok:lombok:1.18.30'

	implementation 'org.springframework.boot:spring-boot-starter'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-webflux'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

	developmentOnly 'org.springframework.boot:spring-boot-devtools'

	runtimeOnly 'com.h2database:h2'
	implementation 'org.postgresql:postgresql:42.6.0'

	implementation 'org.mapstruct:mapstruct:1.5.5.Final'
	annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.5.Final'
	testAnnotationProcessor 'org.mapstruct:mapstruct-processor:1.5.5.Final'

	testImplementation('org.springframework.boot:spring-boot-starter-test') {
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
	}
	testImplementation 'org.junit.jupiter:junit-jupiter-api:5.5.2'
	testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.5.2'

	implementation 'org.json:json:20240303'
	implementation 'io.jsonwebtoken:jjwt:0.12.6'
	implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.0.4'
	implementation 'org.openapitools:jackson-databind-nullable:0.2.6'
	implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.17.1'

	implementation 'org.apache.tika:tika-core:2.9.1'
	implementation 'org.apache.tika:tika-parsers-standard-package:2.9.1'
	implementation 'javax.annotation:javax.annotation-api:1.3.2'
	implementation 'com.google.code.findbugs:jsr305:3.0.2'
}

bootJar {
	archiveBaseName.set("app")
	archiveVersion.set("")
	archiveFileName.set("app.jar")
}

sonar {
	properties {
		property "sonar.projectKey", "therapist-app_backend"
		property "sonar.organization", "therapist-app"
		property "sonar.host.url", "https://sonarcloud.io"
		property "sonar.coverage.exclusions", "=**/*"
	}
}

test {
	useJUnitPlatform()
	testLogging.showStandardStreams = true
	maxParallelForks = 1
}

defaultTasks 'bootJar', 'build'

spotless {
	java {
		target 'src/main/**/*.java'
		importOrder 'java', 'javax', '', 'ch.uzh.ifi.imrg'
		removeUnusedImports()
		trimTrailingWhitespace()
		endWithNewline()
		googleJavaFormat('1.27.0')
	}
}

sourceSets {
	main {
		java {
			srcDir "$projectDir/src/generated/java/src/main/java"
		}
	}
}

def openApiSpecUrl = 'https://backend-patient-app-main.jonas-blum.ch/v3/api-docs'

tasks.named('openApiGenerate') {

	outputs.upToDateWhen { false }

	doFirst {
		logger.lifecycle("üßπ Cleaning OpenAPI output directory for fresh generation...")
		delete("$projectDir/src/generated")
	}

	onlyIf {
		if (project.hasProperty('skipOpenApiGenerate') && project.property('skipOpenApiGenerate') == 'true') {
			logger.lifecycle("‚è≠Ô∏è Skipping openApiGenerate: -PskipOpenApiGenerate flag was set to true.")
			return false
		}
		try {
			def connection = new URL(openApiSpecUrl).openConnection() as HttpURLConnection
			connection.setRequestMethod("HEAD")
			connection.setConnectTimeout(5000)
			connection.setReadTimeout(5000)
			connection.connect()
			boolean reachable = connection.getResponseCode() == 200
			if (!reachable) {
				logger.warn("‚ö†Ô∏è Skipping openApiGenerate: URL is not reachable (HTTP Status: ${connection.getResponseCode()}).")
			}
			return reachable
		} catch (Exception e) {
			logger.warn("‚ö†Ô∏è Skipping openApiGenerate: Could not connect to OpenAPI URL. Reason: ${e.message}")
			return false
		}
	}

	generatorName = 'java'
	remoteInputSpec = openApiSpecUrl
	outputDir = "$projectDir/src/generated/java"
	apiPackage = 'ch.uzh.ifi.imrg.generated.api'
	modelPackage = 'ch.uzh.ifi.imrg.generated.model'
	invokerPackage = 'ch.uzh.ifi.imrg.generated.invoker'
	modelNameSuffix = "PatientAPI"
	apiNameSuffix = "PatientAPI"

	typeMappings = ["time": "Instant", "DateTime": "Instant"]
	importMappings = ["Instant": "java.time.Instant"]

	configOptions = [
			library: 'webclient',
			dateLibrary: 'java8',
			useTags: 'true',
			hideGenerationTimestamp: 'true',
	]

	skipValidateSpec.set(true)
	generateModelTests.set(false)
	generateApiTests.set(false)
	generateModelDocumentation.set(false)
	generateApiDocumentation.set(false)

}

tasks.named('compileJava') {
	dependsOn tasks.named('openApiGenerate')
}